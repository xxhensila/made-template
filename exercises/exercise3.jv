// Chain of computing steps.
// 2 pipes in sqlite.
pipeline CountryPipeline{

    WorldDevelopmentStatsExtractor
        -> WorldDevelopmentStatsTextXLSXInterpreter
        -> CountrySheetPicker
        -> NameHeaderWriter
        -> WorldDevelopmentDataSelector
        ->WorldDevelopmentStatsGDPTableInterpreter  
        ->WorldDevelopmentStatsGDPLoader;
//   pipe 2
     WorldDevelopmentDataSelector
        -> WorldDevelopmentStatsBISTableInterpreter
        -> WorldDevelopmentStatsBISLoader;

    // Extracts a File from the web.
    block WorldDevelopmentStatsExtractor oftype HttpExtractor {

        url: "https://thedocs.worldbank.org/en/doc/7d852628d96b9411d43e5d36d5dff941-0050062022/original/Graphs-Chapter-5-02082022.xlsx";
    }

    // Interprets it as XLSX file (direct Workbook transform).
    block WorldDevelopmentStatsTextXLSXInterpreter oftype XLSXInterpreter { 

    }
    
    // Selects a sheet from the Workbook.
    block CountrySheetPicker oftype SheetPicker {
        sheetName: "Figure S5.1.2";
    }

    // Selects data in the range specified
    block WorldDevelopmentDataSelector oftype CellRangeSelector {
        select: range P2:S45;
        }

    // Renames 4 columns.
    block NameHeaderWriter oftype CellWriter {
        at: range P2:S2;  
        write: [
            "Country Code",
            "Economy",
            "GDP per Capita",
            "Bond Issuance Share"
        ];
    }

    // Interprets a Sheet as a Table. First table
    block WorldDevelopmentStatsGDPTableInterpreter oftype TableInterpreter {
            header: true;
            columns: [
            "Country Code" oftype  CountryCodeAlpha3,
            "GDP per Capita" oftype GDPConstraint
         ];
        }

    // Interprets a Sheet as a Table. Second table
    block WorldDevelopmentStatsBISTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "Country Code" oftype  CountryCodeAlpha3 ,
            "Bond Issuance Share" oftype BISConstraint
     ];
    }
    
    // Loads a Table into a SQLite database sink.
    block WorldDevelopmentStatsGDPLoader oftype SQLiteLoader {
            table: "gdpPerCapita";
            file: "country-stats.sqlite";
        }

    // Loads a Table into a SQLite database sink.
    block WorldDevelopmentStatsBISLoader oftype SQLiteLoader {
        table: "bondIssuance";
        file: "country-stats.sqlite";
    }

}
    // Constraints & Primitive Value Types
    // constraint CountryConstraint_Valid on CountryConstraint:
    //             //valid ISO 3166-1 alpha-3 country code
    //             value matches /^[A-Za-z]{3}$/; 

    constraint GDPConstraint_Valid on GDPConstraint:
                //positive decimal
                value > 0;

    constraint BISConstraint_Valid on BISConstraint:
                //constrained positive decimal
                value >= 0 and value <= 1;

    // valuetype CountryConstraint oftype text {
    // constraints: [
    //             CountryConstraint_Valid
    //             ];
    //              }

    valuetype GDPConstraint oftype decimal {
    constraints: [
                GDPConstraint_Valid
                 ];
                 }

    valuetype BISConstraint oftype decimal {
    constraints: [
                BISConstraint_Valid
                 ];
                 }


